plugins {
  id 'java'
  id "org.jetbrains.kotlin.jvm" version '1.8.0'
  id "org.jetbrains.kotlin.plugin.spring" version '1.8.0'
  id 'org.hidetake.swagger.generator' version '2.19.2'
  id 'org.springframework.boot' version '3.0.2'
}

group = 'com.example.sakila'
version = '1.0-SNAPSHOT'
description = 'sakila-payment-service'
java.sourceCompatibility = JavaVersion.VERSION_17

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  apiSpec
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web:2.7.8'
  implementation 'org.flywaydb:flyway-core:5.2.3'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.8'
  implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.8'
  implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
  implementation 'org.postgresql:postgresql:42.2.8'
  implementation 'com.vladmihalcea:hibernate-types-52:2.4.0'
  implementation 'io.swagger:swagger-annotations:1.5.17'
  implementation 'io.springfox:springfox-swagger2:3.0.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.10.0'
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:3.1.4'
  implementation 'org.springframework.kafka:spring-kafka:2.9.5'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0'
  implementation 'org.jetbrains.kotlin:kotlin-reflect:1.8.0'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
  implementation 'org.mockito:mockito-inline:3.3.3'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0'

  testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.8'
  testImplementation 'org.jetbrains.kotlin:kotlin-test:1.8.0'

  apiSpec 'com.example.sakila:sakila-payment-api-spec:1.0-SNAPSHOT'
  swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.29'
}

swaggerSources {
  server {
    inputFile = file "$buildDir/api-spec/open-api-specification.json"
    code {
      language = 'spring'
      configFile = file "$projectDir/swagger-codegen-config.json"
      components = ['models', 'apis']
    }
  }
}

sourceSets {
  main {
    kotlin {
      srcDirs = ['src/main/java', "${swaggerSources.server.code.outputDir}/src/main/java"]
    }
    java { srcDirs = ["${swaggerSources.server.code.outputDir}/src/main/java"] }
  }
  test {
    java {
      srcDirs = ['src/test/java', "${swaggerSources.server.code.outputDir}/src/main/java"]
    }
    kotlin { srcDirs = [] }
  }
}

test {
  useJUnitPlatform()
}

tasks.register('unpackApiSpec', Sync) {
  dependsOn configurations.apiSpec

  from {
    configurations.apiSpec.collect { zipTree(it) }
  }
  into "$buildDir/api-spec"
}

tasks.register('buildDockerImage', Exec) {
  def dockerFile = "Dockerfile"
  def tag = "${project.group}/${project.name}:${project.version}"
  def context = "."
  def jarFile = "./build/libs/${project.name}-${project.version}.jar"

  commandLine 'docker', 'build',
      '-f', "${dockerFile}",
      '-t', "${tag}",
      "${context}",
      '--build-arg', "JAR_FILE=${jarFile}"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    jvmTarget = '17'
  }
}

swaggerSources.server.code.dependsOn unpackApiSpec
compileKotlin.dependsOn swaggerSources.server.code
buildDockerImage.dependsOn build

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
  options.encoding = 'UTF-8'
}
